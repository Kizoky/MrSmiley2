//Credits goes to Nash, owner of Nashgore

const int NASHGORE_BLOODSPOT_DURATION = 1050;
const int NASHGORE_BLOODFLAGS1 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY;
const int NASHGORE_BLOODFLAGS2 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION;
const int NASHGORE_BLOODFLAGS3 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE;

// Base blood
Actor NashGore_BloodBase
{
	Game Doom
	+CORPSE
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOTELEPORT
	+NOBLOCKMONST
	+NOBLOCKMAP
	-SOLID
}

// Emulated regular Doom blood
Actor NashGore_DoomBlood : Blood
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
}

// Modify the internal Blood actor to spawn flying blood
Actor NashGore_Blood : Blood replaces Blood
{
	-RANDOMIZE
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItem("NashGore_DoomBlood", 0, 0, 0, 1)
			TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.1, 6.0) * RandomPick(-1, 1), FRandom(0.0, 6.0), 0, NASHGORE_BLOODFLAGS1, 64)
			TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, FRandom(0.2, 4.0) * RandomPick(1, -1), FRandom(0.2, 4.0) * RandomPick(1, -1), FRandom(0.0, 4.0), 0, NASHGORE_BLOODFLAGS1, 64)
			Stop
	}
}

// Blood actor that flies outwards and leaves trails behind
Actor NashGore_FlyingBlood : NashGore_BloodBase
{
	Radius 8
	Height 2
	Gravity 0.25
	States
	{
		Spawn:
		FlyingNormal:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
			TNT1 A 0 A_Jump(12, "RandomlyDestroy")
			TNT1 A 0 A_Jump(27, "FlyingDecel")
			Loop
		FlyingDecel:
			TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
			TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE)
			Loop
		RandomlyDestroy:
			TNT1 A 0
			Stop
		Crash:
			TNT1 A 0 A_PlaySound("bloodsplat")
			Stop
	}
}

// Blood trails spawned by FlyingBlood
Actor NashGore_FlyingBloodTrail : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.15
	Scale 0.55
	States
	{
		Spawn:
			BTRL ABCD 4 //bright
			Loop
		Crash:
			TNT1 A 0
			Stop
	}
}

// Blood splashers, generates Random splashes
Actor NashGore_BloodSplasher : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.20
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Crash:
			TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
			Stop
	}
}

// Blood spots that temporarily stay on the floor
Actor NashGore_BloodSpot : NashGore_BloodBase
{
	Radius 4
	Height 2
	RenderStyle Shaded
	StencilColor "FF 00 00"
	Alpha 0.85
	Scale 0.5
	-NOBLOCKMAP
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
			TNT1 A 0 A_Jump(256, "BloodSpot1", "BloodSpot2", "BloodSpot3", "BloodSpot4")
		BloodSpot1:
			BSPT A 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot2:
			BSPT B 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot3:
			BSPT C 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		BloodSpot4:
			BSPT D 0 A_SetTics(NASHGORE_BLOODSPOT_DURATION)
			"####" "#" 0 A_Jump(256, "FadeOut")
		FadeOut:
			"####" "#" 2 A_FadeOut(0.005)
			Loop
	}
}

// Blood splashes
Actor NashGore_BloodSplash : NashGore_BloodBase
{
	Radius 1
	Height 2
	Scale 0.5
	-NOBLOCKMAP
	States
	{
		Spawn:
			BSPL ABCDE 5
			Stop
	}
}

// Blood splat decal maker
// TO DO: This only generates red blood decals PLEASE FIX
// http://forum.zdoom.org/viewtopic.php?f=15&t=46146&p=774415#p774415
Actor NashGore_SplatMaker
{
	Radius 4
	Height 8
	PROJECTILE
	States
	{
		Spawn:
			TNT1 A 20
		Death:
			TNT1 A 1
			Stop
	}
}

// EOF