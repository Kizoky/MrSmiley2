//===========================================================================
//
// Baron of Hell // Blue Ranger
//
//===========================================================================
class BlueRanger : SmileyActor
{
	Default
	{
		Health 1000;
		Radius 24;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 50;
		Monster;
		+FLOORCLIP
		+BOSSDEATH
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Obituary "$OB_BARON";
		HitObituary "$OB_BARONHIT";
	}
	States
	{
	Spawn:
		BOSS AB 10 A_Look ;
		Loop;
	See:
		BOSS AABBCCDD 3 A_Chase;
		Loop;
	Melee:
	Missile:
		BOSS EF 8 A_FaceTarget;
		BOSS G 8 A_BruisAttack;
		Goto See;
	Pain:
		TNT1 A 0
		{
			int chance = random(0,100);
			
			if (chance > 50)
				SetStateLabel("Reflect");
		
		}
		BOSS H 2 A_Pain;	
		BOSS H 3;
		BOSS PPP 16;
		BOSS H 1;
		Goto See;
	Reflect:
		BOSS H 2 A_Pain;
		BOSS H 3 A_SetReflectiveInvulnerable;
		BOSS PPP 16 A_CentaurDefend;
		BOSS H 1 A_UnsetReflectiveInvulnerable;
		Goto See;
	Death:
		TNT1 A 0 A_Mirrorize();
		BOSS I  8;
		BOSS J  8 A_Scream;
		BOSS K  8;
		BOSS L  8 A_NoBlocking;
		BOSS MN 8;
		BOSS O -1 A_BossDeath;
		Stop;
	Raise:
		TNT1 A 0 A_Metalization();
		TNT1 A 0 A_ResurrectSphere();
		BOSS O 8;
		BOSS NMLKJI 13;
		TNT1 A 0 A_UnMirrorize();
		Goto See;
	}
}

//===========================================================================
//
// Hell Knight // Red Ranger
//
//===========================================================================
class RedRanger : BlueRanger
{
	Default
	{
		Health 500;
		-BOSSDEATH;
		Translation "199:202=175:178";
		SeeSound "knight/sight";
		ActiveSound "knight/active";
		PainSound "knight/pain";
		DeathSound "redranger/death";
		Obituary "%o was killed by a Red Ranger's Bottle.";
		HitObituary "%o was catched and killed by a Red Ranger.";
	}
	States
	{
	Spawn:
		BOSS AB 10 A_Look;
		Loop;
	See:
		BOSS AABBCCDD 3 A_Chase;
		Loop;
	Melee:
	Missile:
		BOSS EF 8 A_FaceTarget;
		BOSS G 8 A_BruisAttack;
		Goto See;
	Pain:
		BOSS H  2;
		BOSS H  2 A_Pain;
		Goto See;
	Death:
		TNT1 A 0 A_Mirrorize();
		BOSS I  8;
		BOSS J  8 A_Scream;
		BOSS K  8;
		BOSS L  8 A_NoBlocking;
		BOSS MN 8;
		BOSS O -1;
		Stop;
	Raise:
		TNT1 A 0 A_Metalization();
		TNT1 A 0 A_ResurrectSphere();
		BOSS O 8;
		BOSS NMLKJI  8;
		TNT1 A 0 A_UnMirrorize();
		Goto See;
	}
}

class RangerBottle : Actor
{
	Default
	{
		Radius 6;
		Height 16;
		Speed 15;
		FastSpeed 20;
		Damage 8;
		Projectile ;
		+RANDOMIZE
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 1;
		SeeSound "baron/attack";
		DeathSound "baron/shotx";
		Decal "BaronScorch";
	}
	States
	{
	Spawn:
		BAL7 AB 4 BRIGHT;
		Loop;
	Death:
		BAL7 CDE 6 BRIGHT;
		Stop;
	}
}


extend class BlueRanger
{

	action void A_Debug()
	{
		A_printbold("No Deflect");
	}
	action void A_Debug2()
	{
		A_printbold("Deflect");
	}
	action void A_Debug3()
	{
		A_printbold("No Deflect 2");
	}

}